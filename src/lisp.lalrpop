use std::str::FromStr;
use ast::*;

grammar;

pub Exprs: Vec<Ast> = { // (1)
    <v:(<Expr>)*> => v,
};

Expr: Ast = {
    "(" <Exprs> ")" => Ast::SExpr(<>),
    "null" => Ast::Null,
    Num => Ast::Int(<>),
    r#""(?:[^"\\]|\\.)*""# => {
      let val = <>;
      Ast::Str(val[1..val.len() - 1].to_owned())
    },
    r#"'(?:[^'\\]|\\.)*'"# => {
      let val = <>;
      Ast::Str(val[1..val.len() - 1].to_owned())
    },
    r";[^;\n\r]*" => Ast::Null,
    Term => Ast::Atom(<>),
};

Term: String = {
    r#"[A-Za-z_?+*/.=<>-]+[0-9A-Za-z_?+*/.=<>-]*"# => (<>).to_owned()
};

Num: i32 = {
    r"[0-9]+" => i32::from_str(<>).unwrap()
};
